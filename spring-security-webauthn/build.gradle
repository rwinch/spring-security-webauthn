/*
 * Copyright 2002-2024 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.github.rwinch.webauthn'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    javascript {
        canBeConsumed = false
    }
}

dependencies {
    javascript project(path: ':spring-security-javascript', configuration: 'javascript')
    implementation platform(libs.jackson.bom)
    implementation platform(libs.spring.framework.bom)
    implementation platform(libs.spring.security.bom)
    implementation libs.webauthn4j.core
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'org.springframework.security:spring-security-web'
    compileOnly libs.jakarta.servlet
    testImplementation platform(libs.assertj.bom)
    testImplementation platform(libs.junit.bom)
    testImplementation platform(libs.mockito.bom)
    testImplementation libs.hamcrest
    testImplementation libs.jakarta.servlet
    testImplementation libs.jsonassert
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor'
    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.springframework:spring-webmvc'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = rootProject.layout.buildDirectory.dir('repo')
        }
    }
}

def syncJavascript = tasks.register('syncJavascript', Sync) {
    group = 'Build'
    description = 'Syncs the Javascript from the javascript configuration'
    into project.layout.buildDirectory.dir('spring-security-javascript')
    from(configurations.javascript) {
        into 'org/springframework/security'
    }
}


sourceSets {
    main {
        resources {
            srcDirs(syncJavascript)
        }
    }
}

